This indicator visualizes in a straight forward way the distance price is away from the mean in absolute standard deviations (Z-score) over a certain lookback period (can be configured). Additionally I've included a moving average of the distance, the MA type can be configured in the settings.

Personally using this indicator for some of my algo mean reversion strategies. Price reaching the extreme treshold (can be configured in settings, standard is 3) could be seen as a point where price will revert to the mean.

I've included alerts for when price crosses into extreme areas, as well as alerts for when crosses back into 'normal' territory again. Both are also plotted on the indicator through background coloring/shapes.

Since I've learned so much from other developers I've decided to open source the code. Let me know if you have any ideas on how to improve, I'll see if I can implement them.

Enjoy!


//@version=4
// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© Sidders

study(title="[Sidders]Std. Deviation from Mean/MA (Z-score)", shorttitle="[Sidders]Std. Dev. from Mean/MA")

//------------------------------------------------------------------------------
//INPUT SETTINGS
//------------------------------------------------------------------------------
Lookback_period = input(34, title="Lookback period", type=input.integer, group="General Settings")
Source = input(close, title="Data source", group="General Settings")
typeMA1 = input("SMA", "MA Type", options=["EMA", "HMA","SMA", "RMA", "WMA"], group="General Settings")

extreme_treshold_pos = input(3, title="Define extreme Std. Dev. threshold", type=input.integer, group="General Settings")
extreme_treshold_neg = -extreme_treshold_pos

//------------------------------------------------------------------------------
//DEFINITIONS
//------------------------------------------------------------------------------

//Z SCORE - MEAN
basis_calc = sma(Source, Lookback_period)
zscore_Mean = (Source - basis_calc) / stdev(Source, Lookback_period)

//Z SCORE - MOVING AVERAGE
zscore_MA = typeMA1 == "EMA" ? ema(zscore_Mean, Lookback_period) : typeMA1 == "SMA" ? sma(zscore_Mean, Lookback_period) : typeMA1 == "RMA" ? rma(zscore_Mean, Lookback_period) : typeMA1 == "HMA" ? hma(zscore_Mean, Lookback_period) : wma(zscore_Mean, Lookback_period)

//------------------------------------------------------------------------------
//PLOTS
//------------------------------------------------------------------------------

plot(zscore_Mean, color=color.white, title="Std. Deviation - Mean",linewidth=2)
plot(zscore_MA, color=color.aqua, title="Std. Deviation - MA")

crossover_pos = crossover(zscore_Mean,extreme_treshold_neg)
crossover_neg = crossunder(zscore_Mean,extreme_treshold_pos)

plotshape(crossover_pos, style=shape.triangleup, size=size.tiny, location=location.bottom, color=color.lime, title="Cross above threshold")
plotshape(crossover_neg, style=shape.triangledown, size=size.tiny, location=location.top, color=color.red, title="Cross below treshold")

zscore_extreme = zscore_Mean > extreme_treshold_pos ? color.orange : zscore_Mean < extreme_treshold_neg ? color.lime : na
bgcolor(zscore_extreme, title="Above/below extreme treshold")

hline(3,    color=#FF0000, linestyle=hline.style_solid, editable=false)
hline(2,    color=#FF0000, linestyle=hline.style_dotted, editable=false)
hline(1,    color=#FF0000, linestyle=hline.style_dotted, editable=false)
hline(0,    color=color.gray, linestyle=hline.style_solid, editable=false)
hline(-3,   color=#00FF00, linestyle=hline.style_solid, editable=false)
hline(-2,   color=#00FF00, linestyle=hline.style_dotted, editable=false)
hline(-1,   color=#00FF00, linestyle=hline.style_dotted, editable=false)

//------------------------------------------------------------------------------
//ALERTS
//------------------------------------------------------------------------------
alertcondition(zscore_Mean > extreme_treshold_pos, title="Short?")
alertcondition(zscore_Mean < extreme_treshold_neg, title="Long?")

alertcondition(crossover_pos, title="Cross up")
alertcondition(crossover_neg, title="Cross down")
